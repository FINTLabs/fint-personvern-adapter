buildscript {
    ext {
        springBootVersion = '1.5.22.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.21.0'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.runtime
    options.windowTitle = 'FINT Java SSE Adapter Skeleton'
    options.memberLevel = JavadocMemberLevel.PRIVATE
    destinationDir = file('./javadocs/')
}

import org.apache.tools.ant.filters.ReplaceTokens

afterEvaluate {
    configure(processResources) {
        filesMatching('**/*.yml') {
            filter(ReplaceTokens, tokens: [version: jar.version])
        }
    }
}


sourceCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'https://repo.fintlabs.no/releases'
    }
}

dependencies {
    compile("no.fint:fint-felles-resource-model-java:${apiVersion}")
    compile("no.fint:fint-personvern-resource-model-java:${apiVersion}")

    compile('org.apache.commons:commons-lang3:3.8.1')
    compile('org.projectlombok:lombok:1.18.6')
    compile('com.github.springfox.loader:springfox-loader:1.2.0')
    compile('org.glassfish.jersey.core:jersey-common:2.28')
    compile('org.glassfish.jersey.core:jersey-client:2.28')
    compile('org.jooq:jool-java-8:0.9.14')
    compile('org.apache.commons:commons-text:1.9')

    compile('no.fint:fint-sse:1.4.1')
    compile('no.fint:fint-event-model:3.0.2')
    compile('no.fint:fint-relation-model:1.1.6')
    compile('no.fint:fint-model-resource:0.4.1')
    compile('no.fintlabs:fint-metamodell-model-java:1.1.0')

    compile 'javax.validation:validation-api:2.0.1.Final'
    compile 'org.hibernate.validator:hibernate-validator:6.0.19.Final'

    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    runtime('org.springframework.boot:spring-boot-actuator')

    testCompile('cglib:cglib-nodep:3.2.10')
    testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0')
    testCompile('com.squareup.okhttp3:mockwebserver:4.3.1')
    testCompile('com.squareup.okhttp3:okhttp:4.3.1')
}

task copyExternalDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.Application",
                "Specification-Version": "${version}",
                "Implementation-Version": "${version}",
                "Class-Path": configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

bootRepackage.enabled = false

bootRun {
    systemProperties System.properties
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/v1.6.0/dependencyReport.gradle'
